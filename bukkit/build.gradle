//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } // For BungeeCord missing in spigot
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18-pre8-R0.1-SNAPSHOT'
    implementation(rootProject) {
        exclude group: 'org.slf4j'
    }
    compileOnly 'org.slf4j:slf4j-api:2.0.0-alpha5'
}

tasks.build.dependsOn tasks.shadowJar

processResources {
    filesMatching('plugin.yml') {
        expand 'version': project.version
    }
}

publishing {
    publications {
        main(MavenPublication) {
            artifact jar
            artifact shadowJar
            artifact sourcesJar
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
